// Reduced gambler's ruin model in which the gambler chooses the stakes.
// From Sutton and Barto.

mdp

const int Wealth = 8;   // initial wealth
const double p   = 1/2; // probability of winning

label "ruin" = x = 0;
label "goal" = x = 16;

module gambler
  x : [0..16] init Wealth;

  [b0] x=0 | x=16 -> true;
  [b1] x>0 & x<16 -> p : (x'=x+1) + (1-p) : (x'=x-1);
  [b2] x>1 & x<15 -> p : (x'=x+2) + (1-p) : (x'=x-2);
  [b3] x>2 & x<14 -> p : (x'=x+3) + (1-p) : (x'=x-3);
  [b4] x>3 & x<13 -> p : (x'=x+4) + (1-p) : (x'=x-4);
  [b5] x>4 & x<12 -> p : (x'=x+5) + (1-p) : (x'=x-5);
  [b6] x>5 & x<11 -> p : (x'=x+6) + (1-p) : (x'=x-6);
  [b7] x>6 & x<10 -> p : (x'=x+7) + (1-p) : (x'=x-7);
  [b8] x>7 & x<9  -> p : (x'=x+8) + (1-p) : (x'=x-8);
endmodule
