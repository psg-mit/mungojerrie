// A variation on the game in "Synthesizing Strategy Improvement and Recursive
// Algorithms for Solving 2.5 Player Parity Games."
//
// The row numbers grow going up.

smg

player R0 [ns0], [es0], [ss0], [ws0], [hlt] endplayer
player R1 [sht], [nop] endplayer

const int N = 7;     // side of square grid
const int M = 3;     // side of each zone
const int i0 = 0;    // initial row and column of robot0
const int i1 = N-1;  // row and column of robot1
const int bi = 1;    // initial number of bullets

const double d = 1/2;   // probability of destruction when shooting point blank

formula hp = pow(d, pow((r0-i1)*(r0-i1)+(c0-i1)*(c0-i1),1/2));

// The four corner zones:
// Upper Left: 0  Upper Right: 1
// Lower Left: 2  Lower Right: 3
label "z00" = !h & (r0 > N-M-1 & c0 < M);
label "z01" = !h & (r0 > N-M-1 & c0 > N-M-1);
label "z02" = !h & (r0 < M   & c0 < M);
label "z03" = !h & (r0 < M   & c0 > N-M-1);

module robot0
  r0 : [0..N-1] init i0;
  c0 : [0..N-1] init i0;

  [ns0] !h & r0<N-1 & (c0 != i1 | r0 + 1 != i1) -> (r0' = r0 + 1);
  [ns0] !h & r0=N-1 | (c0 = i1 & r0 + 1 = i1)   -> true;

  [es0] !h & c0<N-1 & (r0 != i1 | c0 + 1 != i1) -> (c0' = c0 + 1);
  [es0] !h & c0=N-1 | (r0 = i1 & c0 + 1 = i1)   -> true;

  [ss0] !h & r0>0 & (c0 != i1 | r0 - 1 != i1)   -> (r0' = r0 - 1);
  [ss0] !h & r0=0 | (c0 = i1 & r0 - 1 = i1)     -> true;

  [ws0] !h & c0>0 & (r0 != i1 | c0 - 1 != i1)   -> (c0' = c0 - 1);
  [ws0] !h & c0=0 | (r0 = i1 & c0 - 1 = i1)     -> true;

  [hlt] h -> true;
endmodule

module robot1
  h : bool init false;   // robot0 has been hit

  [sht] b > 0 -> hp : (h'=true) + 1-hp : true;
  [nop] true -> true;
endmodule

module control
  t : [0..1] init 0;   // turn
  b : [0..bi] init bi; // remaining bullets

  [ns0] t=0 -> (t'=1);
  [es0] t=0 -> (t'=1);
  [ss0] t=0 -> (t'=1);
  [ws0] t=0 -> (t'=1);
  [hlt] t=0 -> true;

  [sht] t=1 -> (t'=0) & (b' = b - 1);
  [nop] t=1 -> (t'=0);
endmodule
