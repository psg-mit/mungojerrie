// Knuth and Yao's simulation of a fair die by a fair coin.
// From the Probmela of Baier and Katoen, p. 838.

mdp

const int s0    = 0;
const int s123  = 7;
const int s456  = 8;
const int s23   = 9;
const int s123p = 10;
const int s456p = 11;
const int s45   = 12;

label "d1" = state=1;
label "d2" = state=2;
label "d3" = state=3;
label "d4" = state=4;
label "d5" = state=5;
label "d6" = state=6;

module ky
  state : [s0..s45] init s0;

  [toss] state=s0    -> 1/2 : (state'=s123)  + 1/2 : (state'=s456);
  [toss] state=s123  -> 1/2 : (state'=s123p) + 1/2 : (state'=s23);
  [toss] state=s23   -> 1/2 : (state'=2)     + 1/2 : (state'=3);
  [toss] state=s123p -> 1/2 : (state'=s123)  + 1/2 : (state'=1);
  [toss] state=s456  -> 1/2 : (state'=s456p) + 1/2 : (state'=s45);
  [toss] state=s45   -> 1/2 : (state'=4)     + 1/2 : (state'=5);
  [toss] state=s456p -> 1/2 : (state'=s456)  + 1/2 : (state'=6);
  [done] state=1     -> (state'=1);
  [done] state=2     -> (state'=2);
  [done] state=3     -> (state'=3);
  [done] state=4     -> (state'=4);
  [done] state=5     -> (state'=5);
  [done] state=6     -> (state'=6);
endmodule
