// A variation on the game in "Synthesizing Strategy Improvement and Recursive
// Algorithms for Solving 2.5 Player Parity Games."
//
// The row numbers grow going up.

smg

player R0 [ns0], [nd0], [es0], [ed0], [ss0], [sd0], [ws0], [wd0], [hlt] endplayer
player R1 [sht], [nop] endplayer

const int N = 7;     // side of square grid
const int M = 3;     // side of each zone
const int i0 = 0;    // initial row and column of robot0
const int i1 = N-1;  // row and column of robot1
const int bi = 1;    // initial number of bullets

const double p = 1/2;   // probability of completion of long moves
const double q = 1 - p;
const double d = 1/2;   // probability of destruction when shooting point blank

formula hp = pow(d, pow((r0-i1)*(r0-i1)+(c0-i1)*(c0-i1),1/2));

// The four corner zones:
// Upper Left: 0  Upper Right: 1
// Lower Left: 2  Lower Right: 3
label "z00" = r0 > N-M-1 & c0 < M;
label "z01" = r0 > N-M-1 & c0 > N-M-1;
label "z02" = r0 < M   & c0 < M;
label "z03" = r0 < M   & c0 > N-M-1;

module robot0
  r0 : [0..N-1] init i0;
  c0 : [0..N-1] init i0;

  [ns0] !h & r0<N-1 & (c0 != i1 | r0 + 1 != i1) -> (r0' = r0 + 1);
  [ns0] !h & r0=N-1 | (c0 = i1 & r0 + 1 = i1)   -> true;

  [nd0] !h & r0<N-2 & (c0 != i1 | (r0 + 1 != i1 & r0 + 2 != i1)) ->
    p : (r0' = r0 + 2) + q : (r0' = r0 + 1);

  [es0] !h & c0<N-1 & (r0 != i1 | c0 + 1 != i1) -> (c0' = c0 + 1);
  [es0] !h & c0=N-1 | (r0 = i1 & c0 + 1 = i1)   -> true;

  [ed0] !h & c0<N-2 & (r0 != i1 | (c0 + 1 != i1 & c0 + 2 != i1)) ->
    p : (c0' = c0 + 2) + q : (c0' = c0 + 1);

  [ss0] !h & r0>0 & (c0 != i1 | r0 - 1 != i1)   -> (r0' = r0 - 1);
  [ss0] !h & r0=0 | (c0 = i1 & r0 - 1 = i1)     -> true;

  [sd0] !h & r0>1 & (c0 != i1 | (r0 - 1 != i1 & r0 - 2 != i1)) ->
    p : (r0' = r0 - 2) + q : (r0' = r0 - 1);

  [ws0] !h & c0>0 & (r0 != i1 | c0 - 1 != i1)   -> (c0' = c0 - 1);
  [ws0] !h & c0=0 | (r0 = i1 & c0 - 1 = i1)     -> true;

  [wd0] !h & c0>1 & (r0 != i1 | (c0 - 1 != i1 & c0 - 2 != i1)) ->
    p : (c0' = c0 - 2) + q : (c0' = c0 - 1);

  [hlt] h -> true;
endmodule

module robot1
  h : bool init false;   // robot0 has been hit

  [sht] b > 0 -> hp : (h'=true) + 1-hp : true;
  [nop] true -> true;
endmodule

module control
  t : [0..1] init 0;   // turn
  b : [0..bi] init bi; // remaining bullets

  [ns0] t=0 -> (t'=1);
  [nd0] t=0 -> (t'=1);
  [es0] t=0 -> (t'=1);
  [ed0] t=0 -> (t'=1);
  [ss0] t=0 -> (t'=1);
  [sd0] t=0 -> (t'=1);
  [ws0] t=0 -> (t'=1);
  [wd0] t=0 -> (t'=1);
  [hlt] t=0 -> true;

  [sht] t=1 -> (t'=0) & (b' = b - 1);
  [nop] t=1 -> (t'=0);
endmodule
